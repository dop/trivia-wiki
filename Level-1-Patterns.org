First of all, =match1= accepts or1/guard1 patterns only.

: (or1 subpattens*)
: (guard1 symbol
:         test-form {generator-form subpattern}*)

+ Unlike original =guard= pattern, guard1 patterns do not allow subpatterns
  in =symbol=. 1 guard1 pattern corresponds to exactly 1 type checking.
  Instead, =symbol= may contain additional lexical
  information. For details see the next section.
+ Level-1 patterns should be canonical. That is, there should be no
  forward-referenced symbols.

Thus, compilation of =match1= is equivalent to just building a
form consisting of =if= and =let= binding. =match1= assumes the
matching tree is already valid and optimized.

Example:

#+BEGIN_SRC lisp
(match1 '(1 2)
  ((guard1 temp (consp temp)
           (car temp) (guard1 a t)
           (cadr temp) (guard1 b t))
   (vector a b)))

;; --> #(1 2)
#+END_SRC

Note that, since even variable patterns are missing in Level 1,
one should use =(guard1 a t)= to bind the value generated by =(car
temp)=. It produces a code like below.

#+BEGIN_SRC lisp
(LET ((#:WHAT1862 '(1 2)))
  (BLOCK NIL
    (LET ((TEMP #:WHAT1862))
      (WHEN (CONSP TEMP)
        (LET ((#:FIELD1863 (CAR TEMP)))
          (LET ((A #:FIELD1863))
            (WHEN T
              (LET ((#:FIELD1864 (CADR TEMP)))
                (LET ((B #:FIELD1864))
                  (WHEN T (RETURN (LOCALLY (VECTOR A B)))))))))))))
#+END_SRC

** Extended syntax of =guard1=

Normal syntax of guard1:

#+BEGIN_SRC lisp
(guard1 symbol test-form {generator-form subpattern}*)
#+END_SRC

Extended syntax of guard1:

#+BEGIN_SRC lisp
(guard1 (symbol &key (type t) place (ignorable (symbol-package symbol)) &allow-other-keys)
        test-form {generator-form subpattern}*)
#+END_SRC

- type :: a type specifier. It is added to the declaration after =let=.
- place :: if this key is non-nil, then the symbol is bound with
           symbol-macrolet and the accessor becomes setf-able.
- ignorable :: If non-nil, =symbol= is declared to be =ignorable=, and is
               not accounted for or1 pattern consistency check. Default
               value is =T= when =symbol= is a non-interned symbol, and
               =NIL= otherwise.

